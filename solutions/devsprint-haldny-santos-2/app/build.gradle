plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.devpass.spaceapp"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testApplicationId "com.devpass.spaceapp.tests"
        testInstrumentationRunner "com.devpass.spaceapp.tests.AppAndroidTestRunner"
    }

    flavorDimensions "type"

    productFlavors {
        prod {
            dimension "type"
            buildConfigField 'String', 'BASE_URL', '"prod.prod.com"'
        }
        dev {
            dimension "type"
            buildConfigField 'String', 'BASE_URL', '"https://api.spacexdata.com/"'
        }
    }

    buildTypes {
        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled true

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        resources.excludes.add("MANIFEST.MF")
        resources.excludes.add("META-INF/*")
    }

    sourceSets {
        androidTest {
            assets {
                assets.srcDirs = ['src/androidTest/assets']
            }
        }
    }
}

dependencies {
    def lifecycle_version = "2.4.1"
    def retrofit_version = "2.9.0"
    def lottieVersion = "2.8.0"
    def koin_version = "3.2.1"
    def mockk_version = '1.13.2'
    def coroutines_version = '1.6.4'
    def core_testing_version = "2.1.0"
    def test_core_ktx = "1.4.0"

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.activity:activity-ktx:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    testImplementation "androidx.arch.core:core-testing:$core_testing_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    androidTestImplementation "androidx.test:core-ktx:$test_core_ktx"

    androidTestImplementation('com.adevinta.android:barista:4.2.0') {
        exclude group: 'org.jetbrains.kotlin'
    }

    // Cucumber
    androidTestImplementation "io.cucumber:cucumber-android:4.4.1"

    //Mockk
    testImplementation "io.mockk:mockk:$mockk_version"
    androidTestImplementation "io.mockk:mockk-android:$mockk_version"

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    //Maps
    implementation 'com.google.android.gms:play-services-maps:18.0.2'

    //Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.13.2'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.13.2'

    // Lottie
    implementation "com.airbnb.android:lottie:$lottieVersion"

    // koin
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-android:$koin_version"

    // Fragment KTX
    implementation 'androidx.fragment:fragment-ktx:1.4.0'
}
