plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.devpass.spaceapp"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"
        manifestPlaceholders = [apiKey: "$MAPS_API_KEY"]
        testInstrumentationRunner "com.devpass.spaceapp.SpaceAppTestRunner"
    }

    flavorDimensions "type"

    productFlavors {
        prod {
            dimension "type"
            buildConfigField 'String', 'BASE_URL', '"prod.prod.com"'
        }
        dev {
            dimension "type"
            buildConfigField 'String', 'BASE_URL', '"https://api.spacexdata.com/"'
        }
    }

    buildTypes {
        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled true

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
        exclude("META-INF/*.kotlin_module")
    }

    testOptions {
        animationsDisabled = true
    }
}

dependencies {
    def lifecycle_version = "2.4.1"
    def retrofit_version = "2.9.0"
    def lottieVersion = "2.8.0"
    def koin_version = "3.2.1"
    def mockk_version = "1.13.2"
    def coroutines_test_version = "1.6.4"
    def junit_ext_version = "1.1.4"
    def mockito_core_version = "4.8.1"
    def junit_version = "4.8.1"
    def espresso_core_version = "3.5.0"
    def espresso_rule = "1.5.0"
    def espresso_runner = "1.5.1"
    def kotlin_test_version = "1.7.20"
    def android_core_testing = "2.1.0"
    def fragment_version = "1.5.4"

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.activity:activity-ktx:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    //Maps
    implementation 'com.google.android.gms:play-services-maps:18.0.2'

    //Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.13.2'
    kapt 'com.github.bumptech.glide:compiler:4.13.2'

    // Lottie
    implementation "com.airbnb.android:lottie:$lottieVersion"

    // koin
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-android:$koin_version"


    // Fragment KTX
    implementation "androidx.fragment:fragment-ktx:$fragment_version"

    // TDD
    testImplementation project(":test-util:jvm")
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_test_version"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "org.mockito:mockito-core:$mockito_core_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_test_version"
    testImplementation "androidx.arch.core:core-testing:$android_core_testing"

    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core_version"
    androidTestImplementation "androidx.test.espresso:espresso-idling-resource:$espresso_core_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_core_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_core_version"
    androidTestImplementation "androidx.test:runner:$espresso_runner"
    androidTestImplementation "androidx.test:rules:$espresso_rule"
    androidTestImplementation 'androidx.test:core-ktx:1.5.0'
    androidTestImplementation "androidx.test.ext:junit:$junit_ext_version"
    androidTestImplementation "io.mockk:mockk-android:$mockk_version"
}
